apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "cn.eric.myarchitecture_kotlin"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {

            // 加载资源
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)

            // 读取文件
            def keystoreFilepath = properties.getProperty('keystore.path')
            def keyFile = file(keystoreFilepath)

            // 读取字段
            def keystoreAlias = properties.getProperty('keystore.alias')
            def keystoreAliasPw = properties.getProperty('keystore.alias_password')
            def keystorePw = properties.getProperty('keystore.password')

            keyAlias keystoreAlias
            keyPassword keystoreAliasPw
            storeFile keyFile
            storePassword keystorePw
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.kotlin.stdlib

    // Architecture components
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    // Support Library
    implementation deps.support.app_compat
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.constraint_layout
    kapt deps.databing_compier

    // Test Library
    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
    implementation project(':basicore')
}
